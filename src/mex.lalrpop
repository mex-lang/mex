use crate::lexer::{Token, LexicalError};
use crate::ast;

grammar<'input>(source: &'input ast::Source, global: &'input ast::RefScope<'input>);

pub Package: ast::RefScope<'input> = {
    <p: PackageRoot> => {
        ast::Scope::add_space(global.clone(), p)
    },
    <p: PackageNessted> => {
        //let r = ast::Scope::new_package(source.get_name());
        ast::Scope::add_space(global.clone(), p);
        global.clone()
    },
    <root: Package> <p: PackageNessted> => {
        ast::Scope::add_space(root.clone(), p);
        root
    },
    //! => ast::Scope::Error(<>).into()
}

pub PackageItems: Vec<ast::RefScope<'input>> = {
    <p: PackageNessted> => vec![p],
    <mut items: PackageItems> <p: PackageNessted> => {
        items.push(p);
        items
    }
}

pub PackageNessted: ast::RefScope<'input> = {
     "package" <name: Name> "{" <items: PackageItems?> "}" => ast::Scope::new_package(name, items),
     "fragment" <f: Fragment> => ast::Scope::new_model(f),
     "model" <m: Model> => ast::Scope::new_model(m),
     "enum" <i: Enum> => ast::Scope::new_model(i),
     "scalar" <s: Scalar> => ast::Scope::new_model(s),
     ! => ast::Scope::Error(<>).into()
}

pub PackageRoot: ast::RefScope<'input> = {
     "package" <name: Name> ";"? => ast::Scope::new_package(name, None)
}

pub ModelParamsDef: Vec<ast::ModelParamDefinition<'input>> = {
    "<" <items: GenericParamsDef> ">" => items,
    "[" <items: MetadataParamsDef> "]" => items,
    "<" <mut a: GenericParamsDef> ">" "[" <b: MetadataParamsDef> "]" => {
        a.extend(b);
        a
    }
}

pub GenericParamsDef: Vec<ast::ModelParamDefinition<'input>> = {
    <p: GenericParamDef> => vec!(p),
    <mut items: GenericParamsDef> "," <p: GenericParamDef> => {
        items.push(p);
        items
    },
}

pub GenericParamDef: ast::ModelParamDefinition<'input> = {
    <name: Name> => ast::ModelParamDefinition::new_generic(name, None),
    <name: Name> ":" <item_type: ItemType> => ast::ModelParamDefinition::new_generic(name, Some(item_type)),
}

pub MetadataParamsDef: Vec<ast::ModelParamDefinition<'input>> = {
    <p: MetadataParamDef> => vec!(p),
    <mut items: MetadataParamsDef> "," <p: MetadataParamDef> => {
        items.push(p);
        items
    },
}

pub MetadataParamDef: ast::ModelParamDefinition<'input> = {
    <name: Name> ":" <item_type: ItemType> => ast::ModelParamDefinition::new_metadata(name, item_type, None),
    <name: Name> ":" <item_type: ItemType> "=" <value: Literal> => ast::ModelParamDefinition::new_metadata(name, item_type, Some(value))
}

pub ModelParams: Vec<ast::ModelParam<'input>> = {
    "<" <items: GenericParams> ">" => items,
    "[" <items: MetadataParams> "]" => items,
    "<" <mut a: GenericParams> ">" "[" <b: MetadataParams> "]" => {
        a.extend(b);
        a
    }
}

pub GenericParams: Vec<ast::ModelParam<'input>> = {
    <p: GenericParam> => vec!(p),
    <mut items: GenericParams> "," <p: GenericParam> => {
        items.push(p);
        items
    },
}

pub GenericParam: ast::ModelParam<'input> = {
    <item_type: ItemType> => ast::ModelParam::new_generic(item_type)
}

pub MetadataParams: Vec<ast::ModelParam<'input>> = {
    <p: MetadataParam> => vec!(p),
    <mut items: MetadataParams> "," <p: MetadataParam> => {
        items.push(p);
        items
    },
}

pub MetadataParam: ast::ModelParam<'input> = {
    <name: Name> "=" <value: Literal> => ast::ModelParam::new_metadata(name, value)
}

pub Model: ast::ModelDefinition<'input> = {
    <name: Name?> <params: ModelParamsDef?> "{" <items: RecordItems?> "}" => ast::ModelDefinition::new_record(name.into(), items, params),
    <name: Name?> <params: ModelParamsDef?> "(" <items: TupleItems?> ")" ";"? => ast::ModelDefinition::new_tuple(name.into(), items, params),
}

pub ModelInline: ast::ModelDefinition<'input> = {
    <name: Name?> "{" <items: RecordItems?> "}" => ast::ModelDefinition::new_record(name.into(), items, None),
    <name: Name?> "(" <items: TupleItems?> ")" ";"? => ast::ModelDefinition::new_tuple(name.into(), items, None),
}

pub TupleItems: Vec<ast::TupleItem<'input>> = {
    <i: TupleItem> => vec![i],
    <mut items: TupleItems>"," <i: TupleItem> => {
        items.push(i);
        items
    }
}

pub TupleItem: ast::TupleItem<'input> = {
    <t: TupleType> => {
        ast::TupleItem::new_item(t)
    },
    <n: Name> ":" <t: TupleType> => {
        ast::TupleItem::new_named_item(n, t)
    }
}

pub RecordItems: Vec<ast::RecordItem<'input>> = {
    <i: RecordNested> => vec![i],
    <mut items: RecordItems> <i: RecordNested> => {
        items.push(i);
        items
    }
}

pub RecordNested: ast::RecordItem<'input> = {
    <f: RecordField> ","? => f,
    <s: RecordSpred> ","? => s,
}

pub RecordField: ast::RecordItem<'input> = {
    <f: Name> ":" <t: ItemType> => {
        ast::RecordItem::new_item(f, t)
    }
}

pub RecordSpred: ast::RecordItem<'input> = {
    "..." <t: ItemType> => {
        ast::RecordItem::new_spread(t)
    }
}

pub Fragment: ast::ModelDefinition<'input> = {
    <name: Name> <params: ModelParamsDef?> "{" <items: RecordItems?> "}" => ast::ModelDefinition::new_fragment(name, items, params),
}

pub Enum: ast::ModelDefinition<'input> = {
    <name: Name?> <params: ModelParamsDef?> "{" <items: EnunItems> "}" => ast::ModelDefinition::new_enum(name.into(), items, params)
}

pub EnumInline: ast::ModelDefinition<'input> = {
    <name: Name?> "{" <items: EnunItems> "}" => ast::ModelDefinition::new_enum(name.into(), items, None)
}

pub EnunItems: Vec<ast::EnumItem<'input>> = {
    <i: EnumNested> => vec![i],
    <mut items: EnunItems> <i: EnumNested> => {
        items.push(i);
        items
    }
}

pub EnumNested: ast::EnumItem<'input> = {
    <i: EnunItem> ","? => i,
    <i: EnumTuple> ","? => i,
    <i: EnumRecord> ","? => i,
    <i: EnumEnum> ","? => i,
}

pub EnunItem: ast::EnumItem<'input> = {
    <name: Name> => ast::EnumItem::new_item(name)
}

pub EnumRecord: ast::EnumItem<'input> = {
    <name: Name> "{" <items: RecordItems> "}" => ast::EnumItem::new_record(name, items),
}

pub EnumTuple: ast::EnumItem<'input> = {
    <name: Name> "(" <items: TupleItems?> ")" => ast::EnumItem::new_tuple(name, items.unwrap_or(vec![])),
}

pub EnumEnum: ast::EnumItem<'input> = {
    <name: Name> "enum" "{" <items: EnunItems> "}" => ast::EnumItem::new_enum(name, items),
}

pub Scalar: ast::ModelDefinition<'input> = {
    <name: Name> ";"? => ast::ModelDefinition::new_scalar(name)
}

pub Name: ast::Id<'input> = {
    <name: "id"> => ast::Id::Name(name)
}

pub Literal: ast::Literal<'input> = {
    <value: "id"> => ast::Literal::String(value),
    <value: "number"> => ast::Literal::Number(value),
}

pub ItemType: ast::ItemType<'input> = {
    <name: Name> <params: ModelParams?> => ast::ItemType::new_name(name, params),
    <m: ModelInline> => ast::ItemType::new_inline(m),
    "enum" <i: EnumInline> => ast::ItemType::new_inline(i),
}

pub TupleType: ast::ItemType<'input> = {
    <name: Name> <params: ModelParams?> => ast::ItemType::new_name(name, params),
    "(" <items: TupleItems?> ")" => ast::ItemType::new_inline_tuple(items.unwrap_or(vec![])),
}

extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token<'input> {
    "package" => Token::KeywordPackage,
    "model" => Token::KeywordModel,
    "enum" => Token::KeywordEnum,
    "fragment" => Token::KeywordFragment,
    "scalar" => Token::KeywordScalar,

    "id" => Token::Identifier(<&'input str>),
    "number" => Token::Number(<&'input str>),

    "(" => Token::LParen,
    ")" => Token::RParen,
    "{" => Token::LBracket,
    "}" => Token::RBracket,
    "<" => Token::Lees,
    ">" => Token::Greater,
    "[" => Token::LSquare,
    "]" => Token::RSquare,

    "?" => Token::Question,
    "=" => Token::Assign,
    ";" => Token::Semicolon,
    ":" => Token::Colon,
    "," => Token::Comma,
    "..." => Token::Spread,

    "error" => Token::Error(<LexicalError>),
  }
}

// use lalrpop_util::{ErrorRecovery, ParseError};
//    <err: "error"> => {
//        let error = ErrorRecovery {
//            error: ParseError::User {
//                error: LexicalError::InvalidToken,
//            },
//            dropped_tokens: Vec::new(), // or specify the dropped tokens
//        };
//        errors.push(error);
//        Box::new(ast::Scope::Error)
//    },